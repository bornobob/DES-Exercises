grammar lego.rover.Mission with org.eclipse.xtext.common.Terminals

generate mission "http://www.rover.lego/Mission"

Missions:
	missions += Mission+
;

Mission:
	name = ID ':'
	actions += Action+
	('Celebration:' celebration = Celebration)?
;

Celebration:
	DanceCelebration |
	SpeakCelebration
;

DanceCelebration:
	{DanceCelebration}
	'Dance'
;

SpeakCelebration:
	'Speak' toSpeak = STRING
;

Action:
	type = ActionType
	(priority = Priority)?
;

Priority:
	'Priority:' priority = INT
;

ActionType:
	BorderAction |
	CollisionAction |
	ColorDetAction |
	DriveAction |
	UltrasoundAction |
	DontDrownAction |
	MeasureAction |
	PushRockAction 
;

enum Boolean:
	True = "true" | False = "false"
;

Rotation:
	rotation = DECIMAL
;

enum Color:
	Black | Blue | Green | Yellow | Red | White | Brown
;

DECIMAL: INT '.' INT;

Speed:
	speed = INT
;

PushRockAction:
	'PushRocks' 'RocksToPush:' nr_rocks = INT
;

MeasureAction:
	'MeasureLakes' 'LakeColors:' '[' colors += Color+ ']'
;

DontDrownAction:
	'DontDrown'
	'LakeColors:' '[' colors += Color+ ']'
	('Rotations:' rotation = Rotation)?
;

DriveAction:
	{DriveAction}
	'Drive'
	('Speed:' speed = Speed)?
;

UltrasoundAction:
	{UltrasoundAction}
	'UltraSound'
	('Rotations:' rotation = Rotation)?
	('DodgeRocks:' dodgeRocks = Boolean)?
;

ColorDetAction:
	'ColorDetection'
	'Colors:' '[' colors += Color+ ']'
;

CollisionAction:
	{CollisionAction}
	'CollisionDetection'
	('Rotations:' rotation = Rotation)?
;

BorderAction:
	{BorderAction}
	'BorderDetection'
	('Rotations:' rotation = Rotation)?
;